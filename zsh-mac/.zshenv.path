#- [mac]PATH environment settings

# for openssl 1.1 (required: homebrew)
if [[ "${IS_M1_MAC}" = "true" ]]
then
  export HOMEBREW_BASE_DIR=/opt/homebrew
else
  export HOMEBREW_BASE_DIR=/usr/local
fi
export HOMEBREW_OPT_DIR=$HOMEBREW_BASE_DIR/opt
export OPENSSL11_BREW_ROOT=$HOMEBREW_OPT_DIR/openssl@1.1
export MYSQLCLIENT80_BREW_ROOT=$HOMEBREW_OPT_DIR/mysql-client@8.0
export MYSQL57_BREW_ROOT=$HOMEBREW_OPT_DIR/mysql@5.7
export POSTGRES_BREW_ROOT=$HOMEBREW_OPT_DIR/libpq

export ANDROID_SDK="/Applications/Android Studio.app/sdk"
export GOROOT=/usr/local/go
export GOPATH=$HOME/go
export GOBIN=$GOPATH/bin
export ASDF_ROOT=$HOME/.asdf
export GCP_SDK_HOME=$HOME/google-cloud-sdk
export FLUTTER_ROOT=$HOME/flutter
export CALIBRE_APP=/Applications/calibre.app
export TAILSCALE_APP=/Applications/Tailscale.app

#-- zshではシェル変数として"path"を定義すると環境変数PATHと自動連動する
#-- "path"以外に, "cdpath","fpath","manpath" の3つも同様に自動連動
#-- (N-/) を付けた場合、該当パスが存在しなければ無視してくれる
path=(
    $HOME/bin(N-/)
    # Omnisharp by NeoBundle of vim
    $HOME/.bundle/Omnisharp/server/OmniSharp/bin/Debug(N-/)
    $HOME/.cabal/bin(N-/)
    $HOME/.ebcli-virtual-env/executables(N-/)  # AWS eb-cli
    $GCP_SDK_HOME/bin(N-/)
    $ANDROID_SDK/platform-tools(N-/)
    $FLUTTER_ROOT/bin(N-/)
    $CALIBRE_APP/Contents/MacOS(N-/)
    $TAILSCALE_APP/Contents/MacOS(N-/)
    $GOBIN(N-/)
    $GOROOT/bin(N-/)
    $OPENSSL11_BREW_ROOT/bin(N-/)  # See: .zshenv.common
    $MYSQLCLIENT80_BREW_ROOT/bin(N-/)
    $MYSQL57_BREW_ROOT/bin(N-/)
    $POSTGRES_BREW_ROOT/bin(N-/)
    $HOMEBREW_BASE_DIR/bin(N-/)
    /usr/local/mysql/bin(N-/)
    /usr/local/redis/bin(N-/)
    {/usr/local,/usr,}/bin(N-/)
    {/usr/local,/usr,}/sbin(N-/)
    )
fpath=($HOME/zsh-functions(N-/) $fpath)
manpath=({$HOME/local,/usr/local,/usr}/share/man(N-/))
sudo_path=({,/usr,/usr/local}/sbin(N-/))

#- typeset = declare
#-- typeset -U : 重複要素削除
typeset -U path
typeset -U manpath
#-- typeset -xT : -Tで(シェル変数と環境変数の)2変数の連動関係を築く, -xで環境変数として自動export(第1引数を)
typeset -xT SUDO_PATH sudo_path
typeset -U sudo_path

#- cdpath
#-- `cd hoge/huga` で $GOPATH/src/github.com/hoge/huga へcd出来るようにする
cdpath=(
    $GOPATH/src/github.com/goldeneggg(N-/)
    $GOPATH/src/github.com(N-/)
    $GOPATH/src/code.google.com/p(N-/)
)

if [[ -f ${GCP_SDK_HOME}/path.zsh.inc ]]
then
  source ${GCP_SDK_HOME}/path.zsh.inc
fi

if [[ -f ${GCP_SDK_HOME}/completion.zsh.inc ]]
then
  source ${GCP_SDK_HOME}/completion.zsh.inc
fi

# direnv
[[ `which direnv` ]] && eval "$(direnv hook zsh)"
